buildscript {
    ext.kotlin_version = "1.2.41"
    ext.shadow_version = "2.0.1"
    ext.kotson_version = "2.5.0"
    ext.spigot_version = "1.12.2-R0.1-SNAPSHOT"
    ext.grgit_version = "1.1.0"
    ext.kotlintest_version = "2.0.7"

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
        classpath "org.ajoberstar:grgit:$grgit_version"
    }
}

import org.ajoberstar.grgit.Grgit

group "me.minidigger"
version "1.0-SNAPSHOT"

apply plugin: "kotlin"
apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenCentral()
    maven{
        url "https://hub.spigotmc.org/nexus/content/groups/public/"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "com.github.salomonbrys.kotson:kotson:$kotson_version"

    compileOnly "org.spigotmc:spigot-api:$spigot_version"
    testRuntime "org.spigotmc:spigot-api:$spigot_version"

    testCompile "io.kotlintest:kotlintest:$kotlintest_version"
}

ext{
    git = Grgit.open(file('.'))
    revision = git.head().abbreviatedId
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    configurations = [project.configurations.compile]
    manifest {
        attributes 'Implementation-Version': "$project.version@$revision"
    }
}
build.dependsOn shadowJar

task copyToServer(dependsOn: 'jar', type: Copy) {
    from 'build/libs/bettervehicles-' + project.version + '-all.jar'
    into './testserver/plugins/'
}